include(FeatureSummary)
set_package_properties(SDL2_gfx PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_gfx/"
    DESCRIPTION "Support for graphics primitives and transformations with Simple Directmedia Layer"
)

set(SDL2_gfx_FOUND TRUE)

set(SDL2GFX_VENDORED @SDL2GFX_VENDORED@)

set(SDL2GFX_SDL2_REQUIRED_VERSION @SDL_VERSION@)

get_filename_component(CMAKE_CURRENT_LIST_DIR ${CMAKE_CURRENT_LIST_DIR} REALPATH)
get_filename_component(prefix "${CMAKE_CURRENT_LIST_DIR}/@cmake_prefix_relpath@" ABSOLUTE)
set(exec_prefix "@exec_prefix@")
set(bindir "@bindir@")
set(includedir "@includedir@")
set(libdir "@libdir@")
set(_sdl2gfx_extra_static_libraries "@GFX_LIBS@ @PC_LIBS@")
string(STRIP "${_sdl2gfx_extra_static_libraries}" _sdl2gfx_extra_static_libraries)

set(_sdl2gfx_bindir   "${bindir}")
set(_sdl2gfx_libdir   "${libdir}")
set(_sdl2gfx_incdir   "${includedir}/SDL2")

# Convert _sdl2gfx_extra_static_libraries to list and keep only libraries
string(REGEX MATCHALL "(-[lm]([-a-zA-Z0-9._]+))|(-Wl,[^ ]*framework[^ ]*)" _sdl2gfx_extra_static_libraries "${_sdl2gfx_extra_static_libraries}")
string(REGEX REPLACE "^-l" "" _sdl2gfx_extra_static_libraries "${_sdl2gfx_extra_static_libraries}")
string(REGEX REPLACE ";-l" ";" _sdl2gfx_extra_static_libraries "${_sdl2gfx_extra_static_libraries}")

unset(prefix)
unset(exec_prefix)
unset(bindir)
unset(includedir)
unset(libdir)

include(CMakeFindDependencyMacro)

if(NOT TARGET SDL2_gfx::SDL2_gfx)
    if(WIN32)
        set(_sdl2gfx_dll "${_sdl2gfx_bindir}/SDL2_gfx.dll")
        set(_sdl2gfx_imp "${_sdl2gfx_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL2_gfx.dll${CMAKE_STATIC_LIBRARY_SUFFIX}")
        if(EXISTS "${_sdl2gfx_dll}" AND EXISTS "${_sdl2gfx_imp}")
            add_library(SDL2_gfx::SDL2_gfx SHARED IMPORTED)
            set_target_properties(SDL2_gfx::SDL2_gfx
                PROPERTIES
                    IMPORTED_LOCATION "${_sdl2gfx_dll}"
                    IMPORTED_IMPLIB "${_sdl2gfx_imp}"
            )
        endif()
        unset(_sdl2gfx_dll)
        unset(_sdl2gfx_imp)
    else()
        set(_sdl2gfx_shl "${_sdl2gfx_libdir}/${CMAKE_SHARED_LIBRARY_PREFIX}SDL2_gfx${CMAKE_SHARED_LIBRARY_SUFFIX}")
        if(EXISTS "${_sdl2gfx_shl}")
            add_library(SDL2_gfx::SDL2_gfx SHARED IMPORTED)
            set_target_properties(SDL2_gfx::SDL2_gfx
                PROPERTIES
                    IMPORTED_LOCATION "${_sdl2gfx_shl}"
            )
        endif()
    endif()
    if(TARGET SDL2_gfx::SDL2_gfx)
        set_target_properties(SDL2_gfx::SDL2_gfx
            PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${_sdl2gfx_incdir}"
                COMPATIBLE_INTERFACE_BOOL "SDL2_SHARED"
                INTERFACE_SDL2_SHARED "ON"
        )
    endif()
endif()

if(NOT TARGET SDL2_gfx::SDL2_gfx-static)
    set(_sdl2gfx_stl "${_sdl2gfx_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL2_gfx${CMAKE_STATIC_LIBRARY_SUFFIX}")
    if(EXISTS "${_sdl2gfx_stl}")
        add_library(SDL2_gfx::SDL2_gfx-static STATIC IMPORTED)
        set_target_properties(SDL2_gfx::SDL2_gfx-static
            PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${_sdl2gfx_incdir}"
                IMPORTED_LOCATION "${_sdl2gfx_stl}"
                INTERFACE_LINK_LIBRARIES "${_sdl2gfx_extra_static_libraries}"
        )
    endif()
    unset(_sdl2gfx_stl)
endif()

unset(_sdl2gfx_extra_static_libraries)
unset(_sdl2gfx_bindir)
unset(_sdl2gfx_libdir)
unset(_sdl2gfx_incdir)
